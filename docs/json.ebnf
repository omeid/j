// 
//  Based on https://www.ietf.org/rfc/rfc4627.txt
//  The ABNF specifiction used in the mentioned RFC is included
//  for reference, see json.abnf
// 

JSON = Object | Array  .

BeginArray     =  WS "[" WS . //  [ left  square bracket 
EndArray       =  WS "]" WS . //  ] right square bracket 

BeginObject    = WS "{" WS .  //  { left  curly bracket 
EndObject      = WS "}" WS .  //  } right curly bracket 

NameSeparator  = WS ":" WS .  //  : colon 0x3A 

ValueSeparator = WS "," WS .  //  , comma 0x2C 


//  Space (0x20) | Horizontal tab (0x09) | Line feed or New line 0x0A | Carriage return (0x0D)
WS = [ " "  | "\t" | "\n" | "\r" ] .

Value = False  |
        Null   |
        True   |
        Object |
        Array  |
        Number |
        String .

False = "false" .  //  false 0x66.61.6c.73.65 
Null  = "null"  .  //  null  0x6e.75.6c.6c    
True  = "true"  .  //  true  0x74.72.75.65    

Object = BeginObject 
            [ Member { ValueSeparator Member } ]
         EndObject .

Member = String NameSeparator Value .

Array = BeginArray 
            [ Value { ValueSeparator Value } ]
        EndArray .

Number        = [ Minus ] Int [ Frac ] [ Exp ] .

DecimalPoint  = "." .        //  .    0x2E      
NonZeroDigit  = "1" ‚Ä¶ "9" .  //  1-9  0x32-39   

Digit = Zero | NonZeroDigit  .
Exp   = E [ Minus | Plus ] NonZeroDigit { NonZeroDigit } . 
Frac  = DecimalPoint Digit { Digit } .
Int   = Zero | NonZeroDigit { Digit } .

E     = "e" | "E" . //  e|E  0x65  0x45i 
Minus = "-" .       //   - 0x2D 
Plus  = "+" .       //   + 0x2B 
Zero  = "0" .       //   0 0x30 

String = QuotationMark { Char } QuotationMark .

Char = Unescaped | Escaped .

Escaped = Escape (`"`  |  `\` |  "/"  |  "b"  |  "f"  |  "n"  |  "r"  |  "t"  | HexLiteral ) .

HexLiteral    = "u" HexDigit HexDigit HexDigit HexDigit . //  uXXXX 
HexDigit      =   "0" ‚Ä¶ "9" | "A" ‚Ä¶ "F" | "a" ‚Ä¶ "f" .

Escape        = `\`  .     //   \ 0x5C 
QuotationMark = `"`  .     //    0x22 

Unescaped     = " " ‚Ä¶ "!"  |
                "#" ‚Ä¶ "["  | 
                "]" ‚Ä¶ "êøø"   .   //  0x20-21 | 0x23-5B | 0x5D-10FFF
